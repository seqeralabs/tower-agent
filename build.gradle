/*
 * Copyright (c) 2021, Seqera Labs.
 *
 *  This Source Code Form is subject to the terms of the Mozilla Public
 *  License, v. 2.0. If a copy of the MPL was not distributed with this
 *  file, You can obtain one at http://mozilla.org/MPL/2.0/.
 *
 *  This Source Code Form is "Incompatible With Secondary Licenses", as
 *  defined by the Mozilla Public License, v. 2.0.
 */

import org.gradle.nativeplatform.platform.internal.DefaultNativePlatform

plugins {
    alias(libs.plugins.licenser)
    alias(libs.plugins.graalvmNative)
    alias(libs.plugins.micronautApplication)
    alias(libs.plugins.shadow)
}

version = project.file('VERSION').text.strip()
group = "io.seqera.tower.agent"

repositories {
    mavenCentral()
}

dependencies {
    annotationProcessor(libs.picocliCodegen)
    annotationProcessor(libs.micronautHttpValidation)
    annotationProcessor(libs.micronautInjectJava)
    annotationProcessor(libs.micronautGraal)
    implementation(libs.micronautHttpClient)
    implementation(libs.micronautRuntime)
    implementation(libs.micronautPicocli)
    implementation(libs.micronautRxjava2)
    implementation(libs.micronautRxjava2HttpClient)
    implementation(libs.picocli)
    implementation(libs.javaxAnnotation)
    implementation(libs.javaxInject)
    runtimeOnly(libs.logbackClassic)
    runtimeOnly(libs.snakeyaml)
    compileOnly(libs.graalvmSvm)

    implementation(libs.micronautValidation)
    implementation(libs.micronautJacksonDatabind)

    testImplementation(platform(libs.junitBom))
    testImplementation(libs.junitJupiter)
}

license {
    rule(file('HEADER.txt'))
    exclude('**/*.properties')
    exclude('gradlew')
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
}

compileJava {
    options.release.set(17)
    sourceCompatibility = '21'
    targetCompatibility = '21'
    options.compilerArgs += ["-Aproject=${project.name}"]
    dependsOn buildInfo
}

graalvmNative {
    binaries {
        main {
            imageName = 'tw-agent'
            mainClass = 'io.seqera.tower.agent.Agent'
            configurationFileDirectories.from(file('conf'))
            buildArgs(DefaultNativePlatform.currentOperatingSystem.isLinux() ? ['--static', '--libc=musl'] : [])
            buildArgs.add('--allow-incomplete-classpath')
            buildArgs.add('--report-unsupported-elements-at-runtime')
            buildArgs.add('-H:+AddAllCharsets')
            buildArgs.add('-H:EnableURLProtocols=https,http')
            buildArgs.add('-H:+ReportExceptionStackTraces')
        }

        test {
            verbose = true
        }
    }
}

shadowJar {
    archiveBaseName.set('tw-agent')
    archiveClassifier.set('')
    archiveVersion.set('')
}

micronaut {
    runtime("netty")
    testRuntime("junit5")
    processing {
        incremental(true)
        annotations("io.seqera.tower.agent.*")
    }
}

application {
    mainClass.set("io.seqera.tower.agent.Agent")
}

test {
    useJUnitPlatform()
    dependsOn checkLicenses
}

task buildInfo {
    doLast {
        def version = rootProject.file('VERSION').text.trim()
        def versionApi = rootProject.file('VERSION-API').text.trim()
        def commitId = System.env.getOrDefault("GITHUB_SHA", "unknown").substring(0, 7)
        def info = """\
                    version=${version}
                    versionApi=${versionApi}
                    commitId=${commitId}
                """.stripIndent().toString()
        def f = file("src/main/resources/META-INF/build-info.properties")
        f.parentFile.mkdirs()
        f.text = info
    }
}


